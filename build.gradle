description = "Tapestry5 Contribution - Jongo Module"


apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'

apply plugin: 'idea'
apply plugin: 'eclipse'
//apply plugin: 'checkstyle'

group = 'org.t5.contrib'
version = '1.0.0-SNAPSHOT'

ext {
    libraries = [
            tapestry: "5.4-beta-2",
            slf4j: "1.7.4",
            jongo: "1.0",
            spock: "0.7-groovy-2.0"

    ]

}

sourceCompatibility = 1.7
targetCompatibility = 1.7




repositories {

    mavenCentral()

    // Apache Staging
    maven { url "https://repository.apache.org/content/groups/staging" }

}

configurations {
    provided
}

//checkstyle {
//    configFile = new File(rootDir, "$projectDir/src/checkstyle.xml")
//}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies {
    compile "org.apache.tapestry:tapestry-core:${libraries.tapestry}"
    compile "org.apache.tapestry:tapestry-mongodb:${libraries.tapestry}"

    // Jongo
    compile "org.jongo:jongo:${libraries.jongo}"

    // SLF4J
    compile "org.slf4j:slf4j-api:${libraries.slf4j}"
    compile "org.slf4j:slf4j-log4j12:${libraries.slf4j}"

    // Servlet API
    provided "javax.servlet:servlet-api:2.5"

    // Testing
    testCompile "org.spockframework:spock-core:${libraries.spock}"

    // Embedded Mongo for testing
    testCompile "de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.41"

}


jar {
    baseName = 't5contrib-jongo'
    manifest {
        attributes 'Tapestry-Module-Classes': 'org.t5.contrib.jongo.modules.JongoModule'
    }
}


test {


    useJUnit()

    systemProperties["tapestry.service-reloading-enabled"] = "false"
    systemProperties["tapestry.execution-mode"] = "development"

    maxHeapSize = "600M"

    jvmArgs("-XX:MaxPermSize=256M")

    enableAssertions = true
}

